plugins {
	id 'java'
	id 'com.unascribed.nilgradle' version '1.4.0'
}

// You can change this to be higher if you like. NilLoader itself is compiled
// for Java 8 for compatibility with older versions.
sourceCompatibility = 8
targetCompatibility = 8

group = 'io.github.silverandro._221e'
archivesBaseName = '221E'
version = '0.0.0'

repositories {
	mavenCentral()
	maven {
		// NilLoader and NilGradle are published here
		url 'https://repo.sleeping.town'
		content {
			includeGroup 'com.unascribed'
		}
	}
	maven {
		// If you wish to use Yarn or Intermediary
		url 'https://maven.fabricmc.net'
		content {
			includeGroup 'net.fabricmc'
		}
	}
	maven {
		// If you wish to use mcp_config or compile against Minecraft Forge
		url 'https://maven.minecraftforge.net'
		content {
			includeGroup 'net.minecraftforge'
			includeGroup 'de.oceanlabs.mcp'
		}
	}
}

nil {
	id "_221e"
	standalone true
}

dependencies {
	loader 'com.unascribed:nilloader:1.3.1'
	game nil.minecraft('1.16.5', 'client')

	implementation(project(":common"))

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

mappings {
	work nil.mojmap('1.16.5', 'client')
}

test {
	useJUnitPlatform()
}

// Bug with NilGradle
tasks.register("fixNilGradle") {
	doLast {
		jar.manifest {
			attributes(
					'Premain-Class': 'nilloader.NilAgent',
					'Agent-Class': 'nilloader.NilAgent',
			)
		}
	}
}
nilSetup.finalizedBy(fixNilGradle)

tasks.register("launch") {
	dependsOn(build)
	doLast {
		copy {
			from buildDir.absolutePath + "/libs/"
			into "C:/Users/SilverAndro/AppData/Roaming/PrismLauncher/instances/1.19.3/.minecraft/"
		}
	}
}